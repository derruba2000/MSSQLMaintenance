CREATE TABLE dbo.Table1 
(
    Name        VARCHAR(30),
    [Values]    VARCHAR(128)
)
GO

INSERT INTO dbo.Table1 VALUES ('John', 'val,val2,val3')
INSERT INTO dbo.Table1 VALUES ('Peter', 'val5,val7,val9,val14')
INSERT INTO dbo.Table1 VALUES ('Lesli', 'val8,val34,val36,val65,val71,val')
INSERT INTO dbo.Table1 VALUES ('Amy', 'val3,val5,val99')
GO

SELECT * FROM dbo.Table1;
GO

WITH
L0 AS(SELECT 1 AS c UNION ALL SELECT 1),
L1 AS(SELECT 1 AS c FROM L0 AS A, L0 AS B),
L2 AS(SELECT 1 AS c FROM L1 AS A, L1 AS B),
L3 AS(SELECT 1 AS c FROM L2 AS A, L2 AS B),
Numbers AS(SELECT ROW_NUMBER() OVER(ORDER BY c) AS n FROM L3)
SELECT Name, [1] AS Column1, [2] AS Column2, [3] AS Column3, [4] AS Column4, [5] AS Column5, [6] AS Column6, [7] AS Column7
FROM
(SELECT Name,
        ROW_NUMBER() OVER (PARTITION BY Name ORDER BY nums.n) AS PositionInList,
        LTRIM(RTRIM(SUBSTRING(valueTable.[Values], nums.n, charindex(N',', valueTable.[Values] + N',', nums.n) - nums.n))) AS [Value]
 FROM   Numbers AS nums INNER JOIN dbo.Table1 AS valueTable ON nums.n <= CONVERT(int, LEN(valueTable.[Values])) AND SUBSTRING(N',' + valueTable.[Values], n, 1) = N',') AS SourceTable
PIVOT
(
MAX([VALUE]) FOR PositionInList IN ([1], [2], [3], [4], [5], [6], [7])
) AS Table2
GO

SELECT convert(int, t.crn) as crn
   ,x.CODE_POS_1
   ,x.CODE_POS_2
   ,x.CODE_POS_3
   ,x.CODE_POS_4
   ,x.CODE_POS_5
   ,x.CODE_POS_6
   ,x.CODE_POS_7
   ,x.CODE_POS_8
   ,x.CODE_POS_9
   ,x.CODE_POS_10
FROM PS_TestForOnline t
CROSS APPLY (
	select max(case src.ItemNumber when 1 then src.Item end) as CODE_POS_1
		,max(case src.ItemNumber when 2 then src.Item end) as CODE_POS_2
		,max(case src.ItemNumber when 3 then src.Item end) as CODE_POS_3
		,max(case src.ItemNumber when 4 then src.Item end) as CODE_POS_4
		,max(case src.ItemNumber when 5 then src.Item end) as CODE_POS_5
		,max(case src.ItemNumber when 6 then src.Item end) as CODE_POS_6
		,max(case src.ItemNumber when 7 then src.Item end) as CODE_POS_7
		,max(case src.ItemNumber when 8 then src.Item end) as CODE_POS_8
		,max(case src.ItemNumber when 9 then src.Item end) as CODE_POS_9
		,max(case src.ItemNumber when 10 then src.Item end) as CODE_POS_10
	from dbo.DelimitedSplit8k(t.all_codes_in_one_field,'/') src
	) x;
